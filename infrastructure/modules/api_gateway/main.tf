resource "aws_apigatewayv2_api" "the_gateway" {
    name = "http-api"
    protocol_type = "HTTP"
}

resource "aws_apigatewayv2_integration" "lambda" {
    api_id = aws_apigatewayv2_api.the_gateway.id

    integration_type = "AWS_PROXY"
    integration_uri = var.lambda_invoke_arn
}

resource "aws_apigatewayv2_route" "get_string" {
    api_id = aws_apigatewayv2_api.the_gateway.id
    route_key = "GET /the_string"

    target = "integrations/${aws_apigatewayv2_integration.lambda.id}"
}

resource "aws_apigatewayv2_stage" "default" {
    api_id = aws_apigatewayv2_api.the_gateway.id
    name = "$default"
    auto_deploy = true
}

resource "aws_lambda_permission" "allow_apigw" {
    statement_id  = "AllowAPIGatewayInvoke"
    action = "lambda:InvokeFunction"
    function_name = var.lambda_name
    principal = "apigateway.amazonaws.com"

    source_arn = "${aws_apigatewayv2_api.the_gateway.execution_arn}/*/*"
}

